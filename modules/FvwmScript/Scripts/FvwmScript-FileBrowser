UseGettext
WindowLocaleTitle {File Browser}
WindowSize 360 340		# Taille
Font {}

Init
 Begin
  Set $PWD=(GetScriptArgument 1)
  Set $POS=50
  Set $LOOP=1
  While $LOOP==1 Do
  Begin
   If (StrCopy $PWD $POS $POS)=={/} Then
    Set $LOOP=0
   If $POS==0 Then
    Set $LOOP=0
   Set $POS=(Add $POS -1)
  End
  Set $PWD=(StrCopy $PWD 1 $POS)
  Set $CMD2={cd;cd } $PWD {; pwd}
  Set $PWD=(GetOutput $CMD2 0 0)
  Set $OPT={ }
  Set $CMD={cd } $PWD {; ls} $OPT {-1 -F}
  Set $LS=(GetOutput $CMD 1 -1)
  Set $ID=2
  While (GetOutput $CMD $ID -1) <> {} Do
  Begin
   Set $LS=$LS {|} (GetOutput $CMD $ID -1)
   Set $ID=(Add $ID 1)
  End
  ChangeTitle 7 $LS
  ChangeTitle 5 $PWD
  ChangeTitle 9 {}
 End

Widget 1
Property
 Size 85 20
 Position 60 300
 Flags NoReliefString
 Type PushButton
 LocaleTitle {OK}
Main
 Case message of
  SingleClic :
  Begin
   SendToScript (GetScriptFather) $PWD {/} (GetTitle 9)
   Quit
  End
End

Widget 2
Property
 Size 85 20
 Position 195 300
 Flags NoReliefString
 Type PushButton
 LocaleTitle {Cancel}
Main
 Case message of
  SingleClic :
  Begin
   Quit
  End
End

Widget 3
Property
 Position 10 10
 Flags NoReliefString
 Size 340 280
 Type Rectangle
Main
 Case message of
  SingleClic :
  Begin
  End
End

Widget 4
Property
 Position 20 15
 Size 320 20
 Flags NoReliefString NoFocus Left
 Type ItemDraw
 LocaleTitle {Path:}
Main
 Case message of
  SingleClic :
  Begin
  End
End

Widget 5
Property
 Position 25 42
 Flags NoReliefString NoFocus Left
 Size 270 0
 Type ItemDraw
 Title {*}
Main
 Case message of
  SingleClic :
  Begin
  End
End

Widget 10
Property
 Position 20 40
 Size 320 20
 Type Rectangle
Main
 Case message of
  SingleClic :
  Begin
  End
End


Widget 6
Property
 Position 21 67
 Flags NoReliefString
 Size 50 18
 Title {up}
 #Icon up.xpm
 Type PushButton
Main
 Case message of
  SingleClic :
  Begin
   Set $CMD={cd } $PWD {/..; pwd}
   Set $PWD=(GetOutput $CMD 0 0)
   Set $CMD={cd } $PWD {; ls} $OPT {-1 -F}
   Set $LS=(GetOutput $CMD 1 -1)
   Set $ID=2
   While (GetOutput $CMD $ID -1) <> {} Do
   Begin
    Set $LS=$LS {|} (GetOutput $CMD $ID -1)
    Set $ID=(Add $ID 1)
   End
   ChangeTitle 7 $LS
   ChangeTitle 5 $PWD
   ChangeTitle 9 {}
  End
End

Widget 11
Property
 Position 91 67
 Flags NoReliefString
 Size 50 18
 Title {home}
 #Icon home.xpm
 Type PushButton
Main
 Case message of
  SingleClic :
  Begin
   Set $PWD={~/}
   Set $CMD={cd } $PWD {; ls} $OPT {-1 -F}
   Set $LS=(GetOutput $CMD 1 -1)
   Set $ID=2
   While (GetOutput $CMD $ID -1) <> {} Do
   Begin
    Set $LS=$LS {|} (GetOutput $CMD $ID -1)
    Set $ID=(Add $ID 1)
   End
   ChangeTitle 7 $LS
   ChangeTitle 5 $PWD
   ChangeTitle 9 {}
  End
End

Widget 12
Property
 Position 180 72
 Flags NoReliefString
 Type CheckBox
 LocaleTitle { Show hidden files}
Main
 Case message of
  SingleClic :
  Begin
   If (GetValue 12) == 1 Then
    Set $OPT={ -A }
   Else
    Set $OPT={ }
   Set $CMD={cd } $PWD {; ls} $OPT {-1 -F}
   Set $LS=(GetOutput $CMD 1 -1)
   Set $ID=2
   While (GetOutput $CMD $ID -1) <> {} Do
   Begin
    Set $LS=$LS {|} (GetOutput $CMD $ID -1)
    Set $ID=(Add $ID 1)
   End
   ChangeTitle 7 $LS
   ChangeTitle 5 $PWD
   ChangeTitle 9 {}  
  End
End

Widget 7
Property
 Position 20 100
 Flags NoReliefString
 Size 320 115
 Type List
 Title {}
Main
 Case message of
  SingleClic :
  Begin
   Set $CMD3={cd } $PWD {; ls} $OPT {-1}
   Set $PWD2=$PWD {/} (GetOutput $CMD3 (GetValue 7) -1)
   Set $CMD2={cd } $PWD {;cd } $PWD2 { 2>/dev/null; pwd}
   Set $PWD2=(GetOutput $CMD2 0 0)
   If $PWD2 <> $PWD Then
   Begin
    Set $PWD = $PWD2
    Set $CMD={cd } $PWD {; ls} $OPT {-1 -F}
    Set $LS=(GetOutput $CMD 1 -1)
    Set $ID=2
    While (GetOutput $CMD $ID -1) <> {} Do
    Begin
     Set $LS=$LS {|} (GetOutput $CMD $ID -1)
     Set $ID=(Add $ID 1)
    End
    ChangeTitle 7 $LS
    ChangeTitle 5 $PWD
    ChangeTitle 9 {}
   End
   Else
   Begin
    ChangeTitle 9 (GetOutput $CMD3 (GetValue 7) -1)
   End
  End
End

Widget 8
Property
 Position 20 220
 Flags NoReliefString NoFocus
 Type ItemDraw
 LocaleTitle {Selected file:}
Main
 Case message of
  SingleClic :
  Begin
  End
End

Widget 9
Property
 Position 20 240
 Flags NoReliefString
 Size 320 20
 Type TextField
 Title {}
Main
 Case message of
  SingleClic :
  Begin
  End
End

